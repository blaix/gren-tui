module UI.Attribute exposing
    ( Attribute
    , apply
    , applyAll
    , color
    , bgColor
    , fontWeight
    , italic
    )

{-| Styles that can be applied to elements.

You will usually pass an array of these as the first argument to your [`UI`](UI) element functions.

## Attributes

@docs Attribute, color, bgColor, fontWeight, italic

## Applying Attributes

_**You shouldn't need these functions in a normal app.**_

gren-tui will apply them for you when it renders your view.
But maybe you're writing a package or doing something weird?
If so, have fun!

@docs apply, applyAll

-}


import Ansi


{-| Base attribute type.
-}
type Attribute
    = Color Ansi.Color
    | BgColor Ansi.Color
    | FontWeight Ansi.FontWeight
    | Italic


{-| Apply attributes to a string.

The string will be returned wrapped in ANSI escape sequences for the specified attributes.

-}
apply : Attribute -> String -> String
apply attr string =
    case attr of
        Color c ->
            Ansi.wrapColor c string

        BgColor c ->
            Ansi.wrapBgColor c string

        FontWeight w ->
            Ansi.wrapFontWeight w string

        Italic ->
            Ansi.wrapItalic string


{-| Apply attributes to all strings in an array.

The array will be returned with all strings wrapped in ANSI escape sequences for the specified attributes.

-}
applyAll : Array Attribute -> String -> String
applyAll attrs str =
    Array.foldr apply str attrs 


{-| Set the text and/or border color of an element.

See [blaix/gren-ansi](https://packages.gren-lang.org/package/blaix/gren-ansi/latest/module/Ansi#Color)
for available color options.

-}
color : Ansi.Color -> Attribute
color = Color


{-| Set the background color of an element.

See [blaix/gren-ansi](https://packages.gren-lang.org/package/blaix/gren-ansi/latest/module/Ansi#Color)
for available color options.

-}
bgColor : Ansi.Color -> Attribute
bgColor = BgColor


{-| Set the font weight of an element.

See [blaix/gren-ansi](https://packages.gren-lang.org/package/blaix/gren-ansi/latest/module/Ansi#FontWeight)
for available font weight options.

-}
fontWeight : Ansi.FontWeight -> Attribute
fontWeight = FontWeight


{-| Set an element's text to italic.
-}
italic : Attribute
italic = Italic
