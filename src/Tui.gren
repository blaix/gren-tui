module Tui exposing
    ( defineProgram
    , Input(..)
    , Program
    )


import Bytes exposing (Bytes)
import Bytes.Decode
import Init
import Node
import Stream exposing (Stream)
import Terminal


-- PROGRAM INIT


type alias Program model msg =
    Node.Program (Model model) (Msg msg)


type alias AppConfig model msg =
    { init : Node.Environment -> Init.Task { model : model, command : Cmd msg }
    , update : msg -> model -> { model : model, command : Cmd msg }
    , subscriptions : model -> Sub msg
    , view : model -> String
    , onInput : (Input -> msg)
    }

defineProgram : AppConfig model msg -> Node.Program (Model model) (Msg msg)
defineProgram appConfig =
    Node.defineProgram
        { init = init appConfig
        , update = update appConfig
        , subscriptions = subscriptions appConfig.subscriptions
        }


type alias Model appModel =
    { stdin : Stream
    , stdout : Stream
    , stderr : Stream
    , appModel : appModel
    }


init : 
    AppConfig model msg 
    -> Node.Environment
    -> Init.Task { model : Model model, command : Cmd (Msg msg) }
init appConfig env =
    Init.await (appConfig.init env) <| \app ->
        Init.await Terminal.initialize <| \termConfig ->
            let
                model =
                    { stdin = env.stdin
                    , stdout = env.stdout
                    , stderr = env.stderr
                    , appModel = app.model
                    }
            in
            Node.startProgram
                { model = model
                , command =
                    Cmd.batch
                        [ Cmd.map AppMsg app.command
                        , view model (appConfig.view app.model)
                        , case termConfig of
                            Just config ->
                                Terminal.setStdInRawMode config.permission True
                            Nothing ->
                                -- TODO: proper error handling for not being in interactive term
                                Stream.sendLine env.stderr "Not running in interactive terminal."
                        ]
                }


-- UPDATE


type Msg appMsg
    = GotStdin Bytes
    | AppMsg appMsg


update : 
    AppConfig model msg 
    -> Msg msg
    -> Model model
    -> { model : Model model, command : Cmd (Msg msg) }
update appConfig msg model =
    case msg of
        GotStdin bytes ->
            let
                input =
                    bytes
                        |> bytesToString
                        -- TODO: use Char instead of String for KeyChar value
                        -- and verify it's actually a character and not a ctrl-character or something
                        |> Maybe.map KeyChar
                        |> Maybe.withDefault UnknownInput

                appResult =
                    appConfig.update
                        (appConfig.onInput input)
                        model.appModel
            in
            { model = { model | appModel = appResult.model }
            , command = Cmd.map AppMsg appResult.command
            }

        AppMsg appMsg ->
            let
                appResult =
                    appConfig.update appMsg model.appModel
            in
            { model =
                { model | appModel = appResult.model }
            , command =
                Cmd.batch
                    [ view model (appConfig.view appResult.model)
                    , Cmd.map AppMsg appResult.command
                    ]
            }


subscriptions : (model -> Sub msg) -> Model model -> Sub (Msg msg)
subscriptions appSubs model =
    Sub.batch
        [ Sub.map AppMsg (appSubs model.appModel)
        , Stream.listen model.stdin GotStdin
        ]


-- VIEW


view : Model model -> String -> Cmd (Msg msg)
view model appViewResult =
    Cmd.batch
        [ resetScreen model
        , Stream.sendLine model.stdout appViewResult
        ]


resetScreen : Model model -> Cmd (Msg msg)
resetScreen model =
    Cmd.batch
        -- unicode escape sequences
        [ Stream.sendString model.stdout "\u{001b}[2J" -- clear
        , Stream.sendString model.stdout "\u{001b}[H" -- move cursor to home
        ]


-- INPUT


type Input
    = KeyChar String
    | UnknownInput


bytesToString : Bytes -> Maybe String
bytesToString bytes =
    bytes
        |> Bytes.width
        |> Bytes.Decode.string
        |> (\decoder -> Bytes.Decode.decode decoder bytes)


