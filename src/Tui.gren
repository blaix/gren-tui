module Tui exposing
    ( defineProgram
    , Input(..)
    , Program
    , Environment
    , exit
    , exitWithCode
    , hideCursor
    )


{-| Model/View/Update for the command line.

Use this module to define a command-line program that uses [the elm architecture](https://guide.elm-lang.org/architecture/).
See [`defineProgram`](#defineProgram) for details, or one of the [examples](https://github.com/blaix/gren-tui/tree/main/examples).

## Defining your program

@docs defineProgram, Program, Environment

## User Input

@docs Input

## Helpers

@docs exit, exitWithCode, hideCursor

-}


import Ansi
import Bytes exposing (Bytes)
import Bytes.Decode
import Bytes.Encode
import FileSystem.Path exposing (Path)
import Init
import Node
import Stream exposing (Stream)
import Task exposing (Task)
import Terminal
import Time
import UI


-- PROGRAM INIT


framesPerSecond : Float
framesPerSecond =
    60.0


{-| Type alias for Tui programs.
-}
type alias Program model msg =
    Node.Program (Model model msg) (Msg msg)


{-| Record alias for all the functions you need to run a Tui program.
-}
type alias AppConfig model msg =
    { init : Environment -> Init.Task { model : model, command : Cmd msg }
    , update : msg -> model -> { model : model, command : Cmd msg }
    , subscriptions : model -> Sub msg
    , view : model -> UI.Element
    , onInput : (Input -> msg)
    }


{-| Holds information about the environment your program is running in.

This will be passed to your init function.
For example:

    init env =
        Node.startProgram
            { model = 
                { maxWidth = env.columns
                , maxHeight = env.rows
                }
            , command =
                Cmd.none
            }

-}
type alias Environment =
    -- Basically merged Node.Environment and Terminal.Configuration
    { platform : Node.Platform
    , cpuArchitecture : Node.CpuArchitecture
    , applicationPath : Path
    , args : Array String
    , stdout : Stream
    , stderr : Stream
    , stdin : Stream
    , colorDepth : Int
    , columns : Int
    , rows : Int
    }


{-| Define your Tui program.

Very similar to a normal [`Node.defineProgram`](https://packages.gren-lang.org/package/gren-lang/node/version/latest/module/Node#defineProgram), except for two special functions:

`view`: Takes your Model and returns a [`UI`](UI) element.
gren-tui will print this to the screen, keeping track of how many lines are
output so it can clear only those lines and print again when the output
changes. This lets you do things like render progress bars, move characters
around the screen, offer menu selections, etc.

`onInput`: Whenever a key is pressed, a message is sent to your app with an
[`Input`](#Input) value. This lets you capture text, trigger events on certain
key presses, etc.
-}
defineProgram :
    { init : Environment -> Init.Task { model : model, command : Cmd msg }
    , update : msg -> model -> { model : model, command : Cmd msg }
    , subscriptions : model -> Sub msg
    , view : model -> UI.Element
    , onInput : (Input -> msg)
    } -> Program model msg
defineProgram appConfig =
    Node.defineProgram
        { init = init appConfig.init
        , update = update appConfig
        , subscriptions = subscriptions appConfig.subscriptions
        }


type alias Model appModel appMsg =
    { stdin : Stream
    , stdout : Stream
    , stderr : Stream
    , appModel : appModel
    , appInitCmd : Maybe (Cmd appMsg)
    , appReady : Bool
    , terminalRows : Int
    , terminalCols : Int
    , terminalPermission : Maybe Terminal.Permission
    , firstRow : Int
    , oldView : UI.Element
    }


init : 
    (Environment -> Init.Task { model : appModel, command : Cmd appMsg })
    -> Node.Environment
    -> Init.Task { model : Model appModel appMsg, command : Cmd (Msg appMsg) }
init appInit env =
    Init.await Terminal.initialize <| \termConfig ->
        let
            termConfigForApp =
                case termConfig of
                    Just tConfig ->
                        { rows = tConfig.rows
                        , columns = tConfig.columns
                        , colorDepth = tConfig.colorDepth
                        }
                    
                    -- We can fake the Nothing values since we won't even run
                    -- the app if term didn't initialize.
                    Nothing ->
                        { rows = 0
                        , columns = 0
                        , colorDepth = 0
                        }

            tuiEnv =
                { platform = env.platform
                , cpuArchitecture = env.cpuArchitecture
                , applicationPath = env.applicationPath
                , args = env.args
                , stdout = env.stdout
                , stderr = env.stderr
                , stdin = env.stdin
                , colorDepth = termConfigForApp.colorDepth
                , columns = termConfigForApp.columns
                , rows = termConfigForApp.rows
                }
            in
            Init.await (appInit tuiEnv) <| \app ->
                let
                    model =
                        { stdin = env.stdin
                        , stdout = env.stdout
                        , stderr = env.stderr
                        , appModel = app.model
                        , appInitCmd = Just app.command
                        , appReady = False
                        , terminalRows = 0
                        , terminalCols = 0
                        , terminalPermission = Nothing
                        , firstRow = 0
                        , oldView = UI.text [] ""
                        }
                in
                Node.startProgram <|
                    case termConfig of
                        Just config ->
                            { model = 
                                { model 
                                    | terminalRows = config.rows 
                                    , terminalCols = config.columns
                                    , terminalPermission = Just config.permission
                                }
                            , command =
                                Terminal.setStdInRawMode config.permission True
                                    |> Task.andThen (\_ -> Stream.sendString env.stdout Ansi.getCursorReport)
                                    |> Task.execute
                            }

                        Nothing ->
                            -- Not in an interactive terminal
                            { model = model
                            , command = 
                                Stream.sendLine env.stderr "gren-tui must be in an interactive terminal"
                                    |> Task.andThen (\_ -> Node.exitWithCode 1)
                                    |> Task.execute
                            }


-- UPDATE


type Msg appMsg
    = GotStdin Bytes
    | TerminalResized Terminal.Size
    | AppMsg appMsg
    | Tick Time.Posix


update : 
    AppConfig appModel appMsg 
    -> Msg appMsg
    -> Model appModel appMsg
    -> { model : Model appModel appMsg, command : Cmd (Msg appMsg) }
update appConfig msg model =
    case msg of
        GotStdin bytes ->
            let
                input =
                    bytesToInput bytes

                doAppUpdate =
                    appUpdate
                        appConfig.update
                        (appConfig.onInput input)
                        model
            in
            case input of
                CtrlC ->
                    { model = model
                    , command = 
                        exit model.stdout
                            |> Task.execute
                    }

                CursorReport row col ->
                    -- The first report is gren-tui getting initial cursor
                    -- position. Subsequent reports were requested by the app.
                    if model.appReady then
                        doAppUpdate
                    else
                        { model =
                            { model
                                | firstRow = row
                                , appReady = True
                            }
                        , command =
                            Stream.sendString model.stdout (Ansi.clearLine Ansi.Up)
                                |> Task.execute
                        }

                _ ->
                    doAppUpdate

        TerminalResized { rows, columns } ->
            let
                oldViewString =
                    UI.toString model.terminalCols model.oldView

                wrappedLines =
                    oldViewString
                        |> String.lines
                        |> Array.map Ansi.width
                        |> Array.any (\w -> w > columns)

                -- It's too complicated to work out how to adjust the firstRow
                -- calculation if lines wrapped. And depending on the terminal
                -- they may wrap before we receive this event, and if that
                -- scrolls the screen, our firstRow will be off, and we'll get
                -- phantom lines from previous renders. So for now, just clear
                -- the screen if lines wrapped :(

                newFirstRow =
                    if wrappedLines then
                        1
                    else
                        model.firstRow

                cmd =
                    if wrappedLines then
                        Task.execute <|
                            Stream.sendString 
                                model.stdout 
                                (Ansi.clearScreen Ansi.Full)
                    else
                        Cmd.none
            in
            { model =
                { model
                    | terminalRows = rows
                    , terminalCols = columns
                    , firstRow = newFirstRow
                    -- force a redraw
                    , oldView = UI.text [] ""
                }
            , command = cmd
            }

        AppMsg appMsg ->
            appUpdate appConfig.update appMsg model

        Tick _ ->
            let
                viewResult =
                    view appConfig.view model
            in
            -- Don't run the app's init cmd until we're ready
            case { ready = model.appReady, appInit = model.appInitCmd } of
                { ready = True, appInit = Just cmd } ->
                    { model =
                        { viewResult.model | appInitCmd = Nothing }
                    , command =
                        Cmd.batch
                            [ viewResult.command
                            , Cmd.map AppMsg cmd
                            ]
                    }

                _ ->
                    viewResult


appUpdate :
    (appMsg -> appModel -> { model : appModel, command : Cmd appMsg })
    -> appMsg
    -> Model appModel appMsg
    -> { model : Model appModel appMsg, command : Cmd (Msg appMsg) }
appUpdate appUpdate_ appMsg model =
    let
        appUpdateResult =
            appUpdate_ appMsg model.appModel

        newModel =
            { model | appModel = appUpdateResult.model }
    in
    { model = newModel
    , command = 
        Cmd.map AppMsg appUpdateResult.command
    }


subscriptions : (appModel -> Sub appMsg) -> Model appModel appMsg -> Sub (Msg appMsg)
subscriptions appSubs model =
    Sub.batch
        [ Stream.listen model.stdin GotStdin
        , Sub.map AppMsg (appSubs model.appModel)
        , if model.appReady then
            Time.every (1000 / framesPerSecond) Tick
          else
            Sub.none
        , case model.terminalPermission of
            Just termPermission ->
                Terminal.onResize termPermission TerminalResized
            Nothing ->
                Sub.none
        ]


-- Tasks


{-| Task to hide the cursor.

Here's an example of using it in init to hide the cursor as soon as the program starts:

    init env =
        Node.startProgram
            { model = {}
            , command = 
                Tui.hideCursor env.stdout
                    |> Task.execute
            }

-}
hideCursor : Stream -> Task Never {}
hideCursor stdout =
    Ansi.hideCursor
        |> Stream.sendString stdout


{-| Task that exits the program.

The Stream argument should be the stdout value you get from [`Environment`](#Environment).
This is needed to clean things up (e.g. restore cursor, unset styles, etc).

The program will exit with a 0 status code.
If you want something other than zero, use [`exitWithCode`](#exitWithCode).

Here's an example of using it in your update function to exit when the user presses the escape key:

    case input of
        Tui.Escape ->
            { model = model
            , command = 
                Tui.exit model.stdout
                    |> Task.execute
            }

-}
exit : Stream -> Task Never {}
exit stdout =
    cleanup stdout
        |> Task.andThen (\_ -> Node.exit)


{-| Task that exits the program with a specific exit code.

The Stream argument should be the stdout value you get from [`Environment`](#Environment).
This is needed to clean things up (e.g. restore cursor, unset styles, etc).

Here's an example of using it in your update function to exit if there was a failure:

    case result of
        Error e ->
            { model = model
            , command = 
                Stream.sendString model.stderr "Failed!"
                    |> Task.andThen (\_ -> Tui.exitWithCode model.stdout 1)
                    |> Task.execute
            }

-}
exitWithCode : Stream -> Int -> Task Never {}
exitWithCode stream code =
    cleanup stream
        |> Task.andThen (\_ -> Node.exitWithCode code)


cleanup : Stream -> Task Never {}
cleanup stream =
    "\n" ++ Ansi.reset ++ Ansi.showCursor
        |> Stream.sendString stream


-- VIEW


view : 
    (appModel -> UI.Element)
    -> Model appModel appMsg
    -> { model : Model appModel appMsg, command : Cmd (Msg appMsg) }
view appView model =
    let
        element =
            appView model.appModel
    in
    if element == model.oldView then
        { model = model
        , command = Cmd.none
        }
    else
        let
            lines =
                UI.toString model.terminalCols element
                    |> String.lines

            numLines =
                lines
                    |> Array.length

            viewString =
                lines
                    |> Array.map (String.append (Ansi.clearLine Ansi.Down))
                    |> String.join "\n"

            newFirstRow =
                adjustFirstRow model numLines
        in
        { model = 
            { model 
                | firstRow = newFirstRow 
                , oldView = element
            }
        , command = 
            Task.execute (render model viewString)
        }


render : Model appModel appMsg -> String -> Task Never {}
render model viewString =
    -- IMPORTANT!
    --   * Draw over the old view, don't clear then draw, or you'll get screen flicker.
    --   * Draw from the model's OLD first row, or you'll draw over non-tui terminal lines instead of scrolling.
    viewString
        |> String.prepend (Ansi.moveTo model.firstRow 0)
        |> String.append (Ansi.clearScreen Ansi.Down)
        |> Stream.sendString model.stdout


-- INPUT


{-| Value sent to your `onInput` message when the program receives input
(a key is pressed, a signal is received, etc.)

Your program will receive keyboard input as the keys are pressed, as
opposed to getting a bunch of characters after Enter is pressed (under the hood
your terminal program is running in "raw mode"). For example, if someone types
"hi" after starting your program, it will receive a message with `KeyChar "h"`
and then another message with `KeyChar "i"`.

`CursorReport` is a special input type you will receive if you include
[`Ansi.getCursorReport`](https://packages.gren-lang.org/package/blaix/gren-ansi/version/2/module/Ansi#getCursorReport)
in your output. Use this to get the current row and column position of the cursor in the terminal
(where the upper-left corner of the window is row 1 column 1).

`CtrlC` is the "abort" signal and gets intercepted by gren-tui to exit your
program. For now you can't depend on receiving this in your app.
-}
type Input
    = KeyChar String
    | Tab
    | Space
    | Return
    | Escape
    | ArrowUp
    | ArrowDown
    | ArrowLeft
    | ArrowRight
    | CursorReport Int Int
    | CtrlC
    | UnknownInput


ctrlC =
    Bytes.Encode.encode (Bytes.Encode.unsignedInt8 3)


bytesToInput : Bytes -> Input
bytesToInput bytes =
    if bytes == ctrlC then
        CtrlC
    else
        bytes
            |> Bytes.length
            |> Bytes.Decode.string
            |> (\decoder -> Bytes.Decode.decode decoder bytes)
            |> Maybe.map stringToInput
            |> Maybe.withDefault UnknownInput


stringToInput : String -> Input
stringToInput string =
    case checkCursorReport string of
        Just cursorReport ->
            cursorReport
        Nothing ->
            if string == Ansi.arrowUp then
                ArrowUp
            else if string == Ansi.arrowDown then
                ArrowDown
            else if string == Ansi.arrowRight then
                ArrowRight
            else if string == Ansi.arrowLeft then
                ArrowLeft
            else if string == Ansi.escape then
                Escape
            else if string == "\t" then
                Tab
            else if string == "\r" then
                Return
            else if string == " " then
                Space
            else
                KeyChar string


checkCursorReport : String -> Maybe Input
checkCursorReport string =
    -- If we send Ansi.getCursorReport, we get back the cursor position (CPR)
    -- in the form ESC[n;mR, where n is the row and m is the column. 
    -- Using this to know where to move when clearing the screen on update.
    let
        parts =
            string
                |> String.split "["
                |> Array.map (String.split ";")
                |> Array.flatten
                |> Array.map (String.split "R")
                |> Array.flatten
    in
    case parts of
        [ "\u{001b}", row, col, "" ] ->
            let
                parsed =
                    { row = String.toInt row
                    , col = String.toInt col
                    }
            in
            case parsed of
                { row = Just r, col = Just c } ->
                    Just (CursorReport r c)
                _ ->
                    Nothing
        _ ->
            Nothing


-- UTIL


{-| Adjust first row to account for scrolling.
-}
adjustFirstRow : Model appModel appMsg -> Int -> Int
adjustFirstRow model numLines =
    let
        scrolledRows =
            model.firstRow + numLines - 1 - model.terminalRows

        firstRowAdjusted =
            if scrolledRows > 0 then
                model.firstRow - scrolledRows
            else
                model.firstRow
    in
    if firstRowAdjusted < 1 then
        1
    else
        firstRowAdjusted
