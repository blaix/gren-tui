module Ansi exposing

    -- Screen
    ( Direction(..)
    , clearScreen
    , clearLine

    -- Color
    , Color(..)
    , wrapColor
    , setColor

    -- Cursor
    , moveUpLines
    , moveDownLines
    , showCursor
    , hideCursor

    -- Keys
    , arrowUp
    , arrowDown
    , arrowLeft
    , arrowRight

    -- Font styling
    , FontWeight(..)
    , wrapFontWeight
    , setFontWeight
    , wrapItalic
    , wrapUnderline
    , wrapStrikeThrough
    , setItalic
    , unsetItalic
    , setUnderline
    , unsetUnderline
    , setStrikeThrough
    , unsetStrikeThrough
    )


{-| ANSI escape sequences.

Functions and strings to control the screen, cursor, and text formatting in
the terminal.

## Color

@docs wrapColor, setColor, Color

## Screen

@docs clearScreen, clearLine, Direction

## Cursor

@docs moveUpLines, modeDownLines, showCursor, hideCursor

## Keys

@docs arrowUp, arrowDown, arrowLeft, arrowRight

-}


prefix : String
prefix =
    "\u{001b}["


-- SCREEN


type Direction
    = Up
    | Down
    | Full


clearScreen : Direction -> String
clearScreen direction =
    prefix ++ (directionString direction) ++ "J"


clearLine : Direction -> String
clearLine direction =
    prefix ++ (directionString direction) ++ "K"


directionString : Direction -> String
directionString direction =
    case direction of
        Down ->
            "0"

        Up ->
            "1"

        Full ->
            "2"


-- COLOR


type Color
    = NoColor
    | Black
    | Red
    | Green
    | Yellow
    | Blue
    | Magenta
    | Cyan
    | White


wrapColor: Color -> String -> String
wrapColor color string =
    String.join ""
        [ setColor color
        , string
        , setColor NoColor
        ]


setColor : Color -> String
setColor color =
    String.join ""
        [ prefix
        , colorCode color
        , "m"
        ]


colorCode : Color -> String
colorCode color_ =
    case color_ of
        NoColor -> "0"
        Black -> "30"
        Red -> "31"
        Green -> "32"
        Yellow -> "33"
        Blue -> "34"
        Magenta -> "35"
        Cyan -> "36"
        White -> "37"


-- FONT STYLING


type FontWeight
    = NormalWeight
    | Bold
    | Faint


wrapFontWeight : FontWeight -> String -> String
wrapFontWeight weight string =
    String.join ""
        [ setFontWeight weight
        , string
        , setFontWeight NormalWeight
        ]


setFontWeight : FontWeight -> String
setFontWeight weight =
    String.join ""
        [ prefix
        , fontWeightCode weight
        , "m"
        ]


fontWeightCode : FontWeight -> String
fontWeightCode weight =
    case weight of
        NormalWeight -> "22"
        Bold -> "1"
        Faint -> "2"


wrapItalic : String -> String
wrapItalic string =
    String.join ""
        [ setItalic
        , string
        , unsetItalic
        ]


wrapUnderline : String -> String
wrapUnderline string =
    String.join ""
        [ setUnderline
        , string
        , unsetUnderline
        ]


wrapStrikeThrough : String -> String
wrapStrikeThrough string =
    String.join ""
        [ setStrikeThrough
        , string
        , unsetStrikeThrough
        ]


setItalic : String
setItalic =
    prefix ++ "3m"


unsetItalic : String
unsetItalic =
    prefix ++ "23m"


setUnderline : String
setUnderline =
    prefix ++ "4m"


unsetUnderline : String
unsetUnderline =
    prefix ++ "24m"


setStrikeThrough : String
setStrikeThrough =
    prefix ++ "9m"


unsetStrikeThrough : String
unsetStrikeThrough =
    prefix ++ "29m"


-- CURSOR


moveUpLines : Int -> String
moveUpLines n =
    if n < 1 then
        ""
    else
        prefix ++ (String.fromInt n) ++ "F"


moveDownLines : Int -> String
moveDownLines n =
    if n < 1 then
        ""
    else
        prefix ++ (String.fromInt n) ++ "E"


hideCursor : String
hideCursor =
    prefix ++ "?25l"


showCursor : String
showCursor =
    prefix ++ "?25h"


-- KEYS


arrowUp : String
arrowUp =
    prefix ++ "A"

arrowDown : String
arrowDown =
    prefix ++ "B"

arrowRight : String
arrowRight =
    prefix ++ "C"

arrowLeft : String
arrowLeft =
    prefix ++ "D"
